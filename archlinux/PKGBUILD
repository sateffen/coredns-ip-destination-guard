# based on: https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=coredns
pkgname=coredns-ip-destination-guard
pkgver=1.12.4
pkgrel=1
pkgdesc="CoreDNS IP-Destination-Guard DNS server"
depends=('nftables')
makedepends=('go' 'make')
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/sateffen/coredns-ip-destination-guard"
license=('MIT')
backup=('etc/coredns-ip-destination-guard/Corefile')
source=(coredns-${pkgver}.tar.gz::https://github.com/coredns/coredns/archive/v${pkgver}.tar.gz
  coredns-ip-destination-guard.service
  Corefile
  plugin.cfg)

sha256sums=('39713a5bfd6fd1a2df1caa7a8296c4327e48c51e964c83d5fea4ed3a5ba4df9f'
  '2b42e692f37925388d7bb8b5c30cf20b35f634ed3478c6c81b2a4c3e19ec20d1'
  'ebaebb5e01cbb0a2ec0fba8d823ad4db58bdd5dca6ac2ed7d93af89c0177956d'
  'cb6abe403b070a30778a8710ce2cd6f6998f0f011bf19bd6fe430cc807cc4898')

prepare() {
  export GOPATH="$srcdir/build"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd "coredns-$pkgver"
  go mod download
}

build() {
  export GOPATH="$srcdir/build"
  # export PATH=$GOPATH/bin:$PATH
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cp plugin.cfg "coredns-$pkgver"

  cd "coredns-$pkgver"
  make coredns
}

package() {
  install -Dm755 "$srcdir/coredns-$pkgver/coredns" "$pkgdir/usr/bin/coredns-ip-destination-guard"
  install -Dm644 "$srcdir/coredns-ip-destination-guard.service" "$pkgdir/usr/lib/systemd/system/coredns-ip-destination-guard.service"
  install -Dm644 "$srcdir/Corefile" "$pkgdir/etc/coredns-ip-destination-guard/Corefile"
  install -Dm644 "$srcdir/build/pkg/mod/github.com/sateffen/coredns-ip-destination-guard@"*"/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/coredns-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-coredns"
}